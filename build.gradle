buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.parchmentmc', name: 'librarian', version: '1.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

version = '0.1'
group = 'com.codeka.justconduits'
archivesBaseName = 'justconduits'

// Mojang ships Java 17 to end users in 1.18+, so we target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    // We use Parchment mappings, which includes parameter names and Javadoc, in addition to the normal
    // minecraft stuff. See: https://github.com/ParchmentMC/Parchment/wiki/Getting-Started
    mappings channel: 'parchment', version: '2021.12.19-1.18.1'

    // Currently, this location cannot be changed from the default.
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                justconduits {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                justconduits {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'justconduits', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                justconduits {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url = 'https://dvs1.progwml6.com/files/maven' } // For JEI
    maven { url = 'https://cursemaven.com' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.1-39.0.59'

    compileOnly fg.deobf("mezz.jei:jei-${jei_version}:api")
    implementation fg.deobf("mezz.jei:jei-${jei_version}")

    // See https://www.cursemaven.com/ for how these work.
    implementation fg.deobf("curse.maven:the-one-probe-245211:3586969")
    runtimeOnly fg.deobf("curse.maven:configured-457570:3559398") // 1.5.1
    runtimeOnly fg.deobf("curse.maven:mcjtylib-233105:3594914") // 1.18-6.0.3
    runtimeOnly fg.deobf("curse.maven:rftools-base-326041:3593702") // 1.18-3.0.0
    runtimeOnly fg.deobf("curse.maven:rftools-power-290209:3593709") // 1.18-4.0.0
    runtimeOnly fg.deobf("curse.maven:cyclic-239286:3594244") // 1.18.1-1.6.9
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "justconduits",
                "Specification-Vendor"    : "codeka",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "codeka",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// Obfuscate when we publish.
publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}
